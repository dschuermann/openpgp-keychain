buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        // NOTE: Always use fixed version codes not dynamic ones, e.g. 0.7.3 instead of 0.7.+, see README for more information
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath files('gradle-witness.jar')
        // bintray dependency to satisfy dependency of openpgp-api lib
        classpath 'com.novoda:bintray-release:0.5.0'

        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}

plugins {
    // ./gradlew dependencyUpdates -Drevision=release"
    id 'com.github.ben-manes.versions' version '0.17.0'
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

subprojects {
    tasks.withType(Test) {
        maxParallelForks = 1
    }
}

// Ignore tests for external bouncycastle
project(':extern:bouncycastle') {
    subprojects {
        // Need to re-apply the plugin here otherwise the test property below can't be set.
        apply plugin: 'java'
        test.enabled = false
    }
}

// SDK Version and Build Tools used by all subprojects
// See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Controlling-Android-properties-of-all-your-modules-from-the-main-project.
ext {
    compileSdkVersion = 26
    buildToolsVersion = '26.0.2'
    supportVersion = "26.1.0"
}
